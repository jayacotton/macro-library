	macro	fmerr
	fmerr	
	gblb	%fmerr
	aif	(%fmerr)&done
%fmerr	setb	1
	jmp	x$sysin
fmerr	equ	$
	lda	errno
	rrc
	rrc
	rrc
	rrc
	ani	0fh
	call 	h$sysin
	lda	errno
	ani	0fh
	call	h$sysin	
	mov	a,c
	cpi	openf
	jz	open_er
	cpi	closef
	jz	close_er
	cpi	srchf
	jz	open_er
	cpi	writef
	jz	noro_er
	cpi	creatf
	jz	noro_er
	cpi	delf
	jz	delet_er
	lxi	d,o$sysin
	mvi	c,printf
	call	bdos
	jmp	0	; bad error no come back.
h$sysin	equ	$
	cpi	10
	jnc	p$sysin
	adi	'0'
	jmp	i$sysin
p$sysin	adi	'A'-10
i$sysin	mov	e,a
	mvi	c,2
	call	bdos
	ret
o$sysin	db	" Unknown i/o error"
	db	cr,lf
	db	'$'
open_er	equ	$
	lxi	d,c$sysin
	mvi	c,printf
	call	bdos
	jmp	0
c$sysin	db	" Open error, file not found"
	db	cr,lf
	db	'$'
close_er equ	$
	lxi	d,d$sysin
	mvi	c,printf
	call	bdos
	ret
d$sysin	db	" Close error, file not closed" 
	db	cr,lf,'$'
delet_er equ	$
	lxi	d,e$sysin
	mvi	c,printf
	call	bdos
	jmp	0
e$sysin	db	" Delete error, file not found"
	db	cr,lf,'$'
noro_er	equ	$
	lxi	d,r$sysin
	mvi	c,printf
	call	bdos
	jmp	0
r$sysin	db	" Disk error, no room"
	db	cr,lf,'$'
errno	db	0
x$sysin	equ	$
&done	anop
	mend
